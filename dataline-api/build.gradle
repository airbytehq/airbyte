plugins {
    id "org.openapi.generator" version "5.0.0-beta"
    id "java-library"
}

def specFile = "$projectDir/src/main/openapi/config.yaml".toString()
openApiGenerate {
    generatorName = "jaxrs-spec"
    inputSpec = specFile
    outputDir = "$buildDir/generated/api/server"

    apiPackage = "io.dataline.api"
    invokerPackage = "io.dataline.api.invoker"
    modelPackage = "io.dataline.api.model"

    importMappings = [
            'SourceSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'     : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            dateLibrary  : "java8",
            generatePom  : "false",
            interfaceOnly: "true"
    ]
}

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask;
task generateApiClient(type: GenerateTask) {
    generatorName = "java"
    inputSpec = specFile
    outputDir = "$buildDir/generated/api/client"

    apiPackage = "io.dataline.api.client"
    invokerPackage = "io.dataline.api.client.invoker"
    modelPackage = "io.dataline.api.client.model"

    importMappings = [
            'SourceSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'     : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    library = "native"

    generateApiDocumentation = false

    configOptions = [
            dateLibrary  : "java8",
            generatePom  : "false",
            interfaceOnly: "true"
    ]
}


import org.openapitools.generator.gradle.plugin.tasks.GenerateTask;
task generateApiDocs(type: GenerateTask) {
    generatorName = "markdown"
    inputSpec = specFile
    outputDir = "$buildDir/generated/api/docs"

    apiPackage = "io.dataline.api.client"
    invokerPackage = "io.dataline.api.client.invoker"
    modelPackage = "io.dataline.api.client.model"

    importMappings = [
            'SourceSpecification'     : 'com.fasterxml.jackson.databind.JsonNode',
            'SourceConfiguration'     : 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationSpecification': 'com.fasterxml.jackson.databind.JsonNode',
            'DestinationConfiguration': 'com.fasterxml.jackson.databind.JsonNode',
    ]

    generateApiDocumentation = false

    configOptions = [
            dateLibrary  : "java8",
            generatePom  : "false",
            interfaceOnly: "true"
    ]

    doLast {
        delete rootProject.file("docs/api")
        copy {
            from outputDir
            include "**/*.md"
            includeEmptyDirs = false
            into rootProject.file("docs/api")
        }
    }
}

dependencies {
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310'

    implementation group: 'io.swagger', name: 'swagger-annotations', version: '1.6.2'

    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

    implementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.1'
}

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/api/server/src/gen/java", "$buildDir/generated/api/client/src/main/java", "$projectDir/src/main/java"
        }
        resources {
            srcDir "$projectDir/src/main/openapi/"
        }
    }
}

compileJava.dependsOn tasks.openApiGenerate, tasks.generateApiClient, tasks.generateApiDocs

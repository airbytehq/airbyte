plugins {
    id "base"
    id "com.github.node-gradle.node" version "2.2.4"
}

node {
    download = true
    version = "14.11.0"
}

npm_run_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    // todo (cgardens) - the plugin seems to ignore this value when the copy command is run. ideally the output would be place in build/app.
    outputs.dir project.buildDir
}

task test(type: NpmTask) {
    dependsOn assemble

    args = ['run', 'test', '--', '--watchAll=false', '--silent']
    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
}

assemble.dependsOn npm_run_build
//build.finalizedBy test

task copyDocs(type: Copy) {
    from "${System.getProperty("user.dir")}/docs/integrations/getting-started"
    into "${buildDir}/docs/getting-started/"
}

copyDocs.dependsOn npm_run_build
assemble.dependsOn copyDocs

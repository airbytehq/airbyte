import org.jsonschema2pojo.SourceType

plugins {
    id "java-library"
    id "com.github.eirnym.js2p" version "1.0"
}

dependencies {
    annotationProcessor libs.bundles.micronaut.annotation.processor
    api libs.bundles.micronaut.annotation

    implementation project(':airbyte-cdk:java:airbyte-cdk:airbyte-commons')
    implementation project(':airbyte-cdk:java:airbyte-cdk:airbyte-json-validation')
}

jsonSchema2Pojo {
    sourceType = SourceType.YAMLSCHEMA
    source = files("${sourceSets.main.output.resourcesDir}/types")
    targetDirectory = new File(project.buildDir, 'generated/src/gen/java/')

    targetPackage = 'io.airbyte.configoss'
    useLongIntegers = true

    removeOldOutput = true

    generateBuilders = true
    includeConstructors = false
    includeSetters = true
    serializable = true
}
tasks.register('generate').configure {
    dependsOn tasks.named('generateJsonSchema2Pojo')
}

test {
    useJUnitPlatform {
        excludeTags 'log4j2-config', 'logger-client'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('log4j2IntegrationTest', Test) {
    useJUnitPlatform {
        includeTags 'log4j2-config'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('logClientsIntegrationTest', Test) {
    useJUnitPlatform {
        includeTags 'logger-client'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

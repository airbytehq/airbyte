plugins {
    id 'java'
}

dependencies {
    implementation 'commons-cli:commons-cli:1.4'
}

// generate seed for each yaml file.
task generateSeed {
    def seeds = [
            [
                "sourceDefinitionId",
                new File(project.projectDir, '/src/main/resources/seed/source_definitions.yaml'),
                new File(project.projectDir, '/src/main/resources/config/STANDARD_SOURCE_DEFINITION')
            ],
            [
                "destinationDefinitionId",
                new File(project.projectDir, '/src/main/resources/seed/destination_definitions.yaml'),
                new File(project.projectDir, '/src/main/resources/config/STANDARD_DESTINATION_DEFINITION')
            ],
    ]
    seeds.each{val ->
        def name = val[0]
        def taskName = "generateSeed$name"
        dependsOn taskName
        task "$taskName"(type: JavaExec) {
            classpath = sourceSets.main.runtimeClasspath

            main = 'io.airbyte.config.init.RepositorySeed'

            // arguments to pass to the application
            args '--id-name'
            args val[0]
            args '--input-path'
            args val[1]
            args '--output-path'
            args val[2]
        }
    }
}

jar.dependsOn(generateSeed)
testClasses.dependsOn(generateSeed)

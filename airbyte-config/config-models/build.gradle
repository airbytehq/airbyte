import org.jsonschema2pojo.SourceType

plugins {
    id "java-library"
    id "com.github.eirnym.js2p" version "1.0"
}

dependencies {
    annotationProcessor libs.bundles.micronaut.annotation.processor
    api libs.bundles.micronaut.annotation

    implementation project(':airbyte-json-validation')
    implementation project(':airbyte-protocol:protocol-models')
    implementation project(':airbyte-commons')
}

jsonSchema2Pojo {
    sourceType = SourceType.YAMLSCHEMA
    source = files("${sourceSets.main.output.resourcesDir}/types")
    targetDirectory = new File(project.buildDir, 'generated/src/gen/java/')

    targetPackage = 'io.airbyte.config'
    useLongIntegers = true

    removeOldOutput = true

    generateBuilders = true
    includeConstructors = false
    includeSetters = true
    serializable = true
}

test {
    useJUnitPlatform {
        excludeTags 'log4j2-config', 'logger-client'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task log4j2IntegrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'log4j2-config'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task logClientsIntegrationTest(type: Test) {
    useJUnitPlatform {
        includeTags 'logger-client'
    }
    testLogging {
        events "passed", "skipped", "failed"
    }
}

Task publishArtifactsTask = getPublishArtifactsTask("$rootProject.ext.version", project)

import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.testing.Test

class AirbytePerformanceTestJavaPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.sourceSets {
            performanceTestJava {
                java {
                    srcDir 'src/test-performance/java'
                }
                resources {
                    srcDir 'src/test-performance/resources'
                }
            }
        }
        project.tasks.named('check').configure {
            dependsOn project.tasks.matching { it.name == 'compilePerformanceTestJavaJava' }
            dependsOn project.tasks.matching { it.name == 'spotbugsPerformanceTestJava' }
        }

        project.configurations {
            performanceTestJavaImplementation.extendsFrom testImplementation
            performanceTestJavaRuntimeOnly.extendsFrom testRuntimeOnly
        }

        def performanceTestJava = project.tasks.register('performanceTestJava', Test) {
            testClassesDirs = project.sourceSets.performanceTestJava.output.classesDirs
            classpath += project.sourceSets.performanceTestJava.runtimeClasspath

            systemProperty "cpuLimit", System.getProperty("cpuLimit")
            systemProperty "memoryLimit", System.getProperty("memoryLimit")
            useJUnitPlatform()
            testLogging() {
                events "passed", "failed"
                exceptionFormat "full"
                showStandardStreams = true
            }

            outputs.upToDateWhen { false }
            maxHeapSize = '3g'
        }
        performanceTestJava.configure {
            mustRunAfter project.tasks.named('check')
            dependsOn project.tasks.matching { it.name == 'assemble' }
        }
    }
}

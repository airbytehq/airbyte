import groovy.io.FileType
import groovy.io.FileVisitResult
import org.gradle.api.GradleException
import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.Exec
import ru.vyarus.gradle.plugin.python.task.PythonTask

class AirbytePythonConfiguration {
    String moduleDirectory
}

class Helpers {
    static addTestTaskIfTestFilesFound(Project project, String testFilesDirectory, String taskName, taskDependencies) {
        """
        This method verifies if there are test files in a directory before adding the pytest task to run tests on that directory. This is needed
        because if there are no tests in that dir and we run pytest on it, it exits with exit code 5 which gradle takes to mean that the process
        failed, since it's non-zero. This means that if a module doesn't need a unit or integration test, it still needs to add a dummy test file
        like:

        ```
        def make_ci_pass_test():
            assert True
        ```

        So we use this method to leverage pytest's test discovery rules (https://docs.pytest.org/en/6.2.x/goodpractices.html#conventions-for-python-test-discovery)
        to selectively run pytest based on whether there seem to be test files in that directory.
        Namely, if the directory contains a file whose name is test_*.py or *_test.py then it's a test.

        See https://github.com/airbytehq/airbyte/issues/4979 for original context
        """
        if (project.file(testFilesDirectory).exists()) {

            project.projectDir.toPath().resolve(testFilesDirectory).traverse(type: FileType.FILES, nameFilter: ~/(^test_.*|.*_test)\.py$/) { file ->
                project.task(taskName, type: PythonTask, dependsOn: taskDependencies) {
                    module = "pytest"
                    command = "-s ${testFilesDirectory} --cov=./ --cov-config=${project.rootProject.file('tools/python/.coveragerc').absolutePath} --cov-report html --cov-report term"
                }
                // If a file is found, terminate the traversal, thus causing this task to be declared at most once
                return FileVisitResult.TERMINATE
            }
        }

        // If the task doesn't exist then we didn't find a matching file. So add an empty task since other tasks will
        // probably rely on this one existing.
        if (!project.hasProperty(taskName)) {
            project.task(taskName) {
                logger.info "Skipping task ${taskName} because ${testFilesDirectory} doesn't exist."
            }
        }
    }
}

class AirbytePythonPlugin implements Plugin<Project> {

    void apply(Project project) {
        def extension = project.extensions.create('airbytePython', AirbytePythonConfiguration)
        def venvDirectoryName = '.venv'
        project.plugins.apply 'ru.vyarus.use-python'

        project.python {
            envPath = venvDirectoryName
            minPythonVersion = '3.7'
            scope = 'VIRTUALENV'
            installVirtualenv = true
            pip 'flake8:3.8.4'
            pip 'black:20.8b1'
            pip 'mypy:0.812'
            pip 'isort:5.6.4'
            pip 'pytest:6.1.2'
            pip 'pytest-cov:2.12.1'
            pip 'pip:21.1.3'
        }

        project.task('isortFormat', type: PythonTask) {
            module = "isort"
            command = ". --settings-file ${project.rootProject.file('tools/python/.isort.cfg').absolutePath}"
        }

        project.task('blackFormat', type: PythonTask) {
            module = "black"
            // the line length should match .isort.cfg
            command = ". --line-length 140"
            dependsOn project.rootProject.licenseFormat
            dependsOn project.isortFormat
        }

        project.task('flakeCheck', type: PythonTask, dependsOn: project.blackFormat) {
            module = "flake8"
            command = ". --config ${project.rootProject.file('tools/python/.flake8').absolutePath}"
        }

        // attempt to install anything in requirements.txt. by convention this should only be dependencies whose source is located in the project.

        if (project.file('requirements.txt').exists()) {
            project.task('installLocalReqs', type: PythonTask) {
                module = "pip"
                command = "install -r requirements.txt"
                inputs.file('requirements.txt')
                outputs.file('build/installedlocalreqs.txt')

                // HACK: makes all integrations depend on installing requirements for bases. long term we should resolve deps and install in order.
                if (project.getPath().startsWith(":airbyte-integrations:connectors")) {
                    dependsOn project.rootProject.getTasksByName("airbytePythonApply", true).findAll { it.project.getPath().startsWith(":airbyte-integrations:bases") }
                }
            }
        } else if (project.file('setup.py').exists()) {
            // If requirements.txt does not exists, install from setup.py instead, assume a dev or "tests" profile exists.
            // In this case, there is no need to depend on the base python modules since everything should be contained in the setup.py.
            project.task('installLocalReqs', type: PythonTask) {
                module = "pip"
                command = "install .[dev,tests]"
            }
        } else {
            throw new GradleException('Error: Python module lacks requirement.txt and setup.py')
        }

        project.task('installReqs', type: PythonTask, dependsOn: project.installLocalReqs) {
            module = "pip"
            command = "install .[main]"
            inputs.file('setup.py')
            outputs.file('build/installedreqs.txt')
        }

        project.task('installTestReqs', type: PythonTask, dependsOn: project.installReqs) {
            module = "pip"
            command = "install .[tests]"
            inputs.file('setup.py')
            outputs.file('build/installedtestreqs.txt')
        }

        Helpers.addTestTaskIfTestFilesFound(project, 'unit_tests', 'unitTest', project.installTestReqs)
        Helpers.addTestTaskIfTestFilesFound(project, 'integration_tests', 'customIntegrationTests', project.installTestReqs)
        if (!project.tasks.findByName('integrationTest')) {
            project.task('integrationTest')
        }
        project.integrationTest.dependsOn(project.customIntegrationTests)

        if (extension.moduleDirectory) {
            project.task('mypyCheck', type: PythonTask) {
                module = "mypy"
                command = "-m ${extension.moduleDirectory} --config-file ${project.rootProject.file('tools/python/.mypy.ini').absolutePath}"
            }

            project.check.dependsOn mypyCheck
        }

        project.task('airbytePythonFormat', type: DefaultTask) {
            dependsOn project.blackFormat
            dependsOn project.isortFormat
            dependsOn project.flakeCheck
        }

        project.task('airbytePythonApply', type: DefaultTask) {
            dependsOn project.installReqs
            dependsOn project.airbytePythonFormat
        }


        project.task('airbytePythonTest', type: DefaultTask) {
            dependsOn project.airbytePythonApply
            dependsOn project.installTestReqs
            dependsOn project.unitTest
        }

        // Add a task that allows cleaning up venvs to every python project
        project.task('cleanPythonVenv', type: Exec) {
            commandLine 'rm'
            args '-rf', "$project.projectDir.absolutePath/$venvDirectoryName"
        }

        // Add a task which can be run at the root project level to delete all python venvs
        if (!project.rootProject.hasProperty('cleanPythonVenvs')) {
            project.rootProject.task('cleanPythonVenvs')
        }
        project.rootProject.cleanPythonVenvs.dependsOn(project.cleanPythonVenv)

        project.assemble.dependsOn project.airbytePythonApply
        project.assemble.dependsOn project.airbytePythonTest
        project.test.dependsOn project.airbytePythonTest
    }
}

import org.gradle.api.Plugin
import org.gradle.api.Project


class AirbyteConnectorAcceptanceTestPlugin implements Plugin<Project> {
        
    void apply(Project project) {
        
        project.task('connectorAcceptanceTest') {
            dependsOn project.build
            dependsOn project.airbyteDocker
            
            if (!project.hasProperty('connectorAcceptanceTestVersion')) {
                project.ext.connectorAcceptanceTestVersion = 'latest'
            }

            if (project.connectorAcceptanceTestVersion == 'dev') {
                project.connectorAcceptanceTest.dependsOn(':airbyte-integrations:bases:connector-acceptance-test:airbyteDocker')
            }

            doFirst {
                if (project.connectorAcceptanceTestVersion != 'dev') {
                    project.exec {
                        def args = [
                            'docker', 'pull', "airbyte/connector-acceptance-test:${project.connectorAcceptanceTestVersion}"
                        ]
                        commandLine args
                    }
                }
                project.exec {
                    def targetMountDirectory = "/test_input"
                    def args = [
                        'docker', 'run', '--rm', '-i',
                        // provide access to the docker daemon
                        '-v', '/var/run/docker.sock:/var/run/docker.sock',
                        // A container within a container mounts from the host filesystem, not the parent container.
                        // this forces /tmp to be the same directory for host, parent container, and child container.
                        '-v', '/tmp:/tmp',
                        // mount the project dir. all provided input paths must be relative to that dir.
                        '-v', "${project.projectDir.absolutePath}:${targetMountDirectory}",
                        '-w', "$targetMountDirectory",
                        '-e', "AIRBYTE_SAT_CONNECTOR_DIR=${project.projectDir.absolutePath}",
                        "airbyte/connector-acceptance-test:${project.connectorAcceptanceTestVersion}",
                    ]
                    if (project.file('integration_tests/acceptance.py').exists()) {
                        // if integration_tests/acceptance.py exists, use it for setup/tear down external test dependencies
                        args.add('-p')
                        args.add('integration_tests.acceptance')
                    }
                    commandLine args
                }
            }

            outputs.upToDateWhen { false }
        }
        


        if (project.hasProperty('airbyteDockerTest')){
            project.connectorAcceptanceTest.dependsOn(project.airbyteDockerTest)
        }

        // make sure we create the integrationTest task once
        if (!project.tasks.findByName('integrationTest')) {
            project.task('integrationTest')
        }

        project.integrationTest.dependsOn(project.connectorAcceptanceTest)
    }
}


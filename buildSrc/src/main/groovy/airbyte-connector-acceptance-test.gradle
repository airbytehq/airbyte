import org.gradle.api.Plugin
import org.gradle.api.Project


class AirbyteConnectorAcceptanceTestPlugin implements Plugin<Project> {
        
    void apply(Project project) {
        
        def connectorAcceptanceTest = project.tasks.register('connectorAcceptanceTest') {
            if (!project.hasProperty('connectorAcceptanceTestVersion')) {
                project.ext.connectorAcceptanceTestVersion = 'latest'
            }

            doFirst {
                if (project.connectorAcceptanceTestVersion != 'dev') {
                    project.exec {
                        def args = [
                            'docker', 'pull', "airbyte/connector-acceptance-test:${project.connectorAcceptanceTestVersion}"
                        ]
                        commandLine args
                    }
                }
                project.exec {
                    def targetMountDirectory = "/test_input"
                    def args = [
                        'docker', 'run', '--rm', '-i',
                        // provide access to the docker daemon
                        '-v', '/var/run/docker.sock:/var/run/docker.sock',
                        // A container within a container mounts from the host filesystem, not the parent container.
                        // this forces /tmp to be the same directory for host, parent container, and child container.
                        '-v', '/tmp:/tmp',
                        // mount the project dir. all provided input paths must be relative to that dir.
                        '-v', "${project.projectDir.absolutePath}:${targetMountDirectory}",
                        '-w', "$targetMountDirectory",
                        '-e', "AIRBYTE_SAT_CONNECTOR_DIR=${project.projectDir.absolutePath}",
                        "airbyte/connector-acceptance-test:${project.connectorAcceptanceTestVersion}",
                    ]
                    if (project.file('integration_tests/acceptance.py').exists()) {
                        // if integration_tests/acceptance.py exists, use it for setup/tear down external test dependencies
                        args.add('-p')
                        args.add('integration_tests.acceptance')
                    }
                    commandLine args
                }
            }

            outputs.upToDateWhen { false }
        }
        connectorAcceptanceTest.configure {
            dependsOn project.tasks.named('assemble')
            if (project.connectorAcceptanceTestVersion == 'dev') {
                dependsOn project(':airbyte-integrations:bases:connector-acceptance-test').tasks.named('assemble')
            }
        }
    }
}


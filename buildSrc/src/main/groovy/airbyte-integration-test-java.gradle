import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.testing.Test

class AirbyteIntegrationTestJavaPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.sourceSets {
            integrationTestJava {
                java {
                    srcDir 'src/test-integration/java'
                }
                resources {
                    srcDir 'src/test-integration/resources'
                }
            }
        }
        project.tasks.named('check').configure {
            dependsOn project.tasks.matching { it.name == 'compileIntegrationTestJavaJava' }
            dependsOn project.tasks.matching { it.name == 'spotbugsIntegrationTestJava' }
        }

        project.configurations {
            integrationTestJavaImplementation.extendsFrom testImplementation
            integrationTestJavaRuntimeOnly.extendsFrom testRuntimeOnly
        }

        def integrationTestJava = project.tasks.register('integrationTestJava', Test) {
            testClassesDirs = project.sourceSets.integrationTestJava.output.classesDirs
            classpath += project.sourceSets.integrationTestJava.runtimeClasspath

            useJUnitPlatform {
                // todo (cgardens) - figure out how to de-dupe this exclusion with the one in build.gradle.
                excludeTags 'log4j2-config', 'logger-client', 'cloud-storage'
            }

            testLogging() {
                events "passed", "failed", "started"
                exceptionFormat "full"
                // uncomment to get the full log output
                // showStandardStreams = true
            }

            outputs.upToDateWhen { false }

            maxParallelForks = project.test.maxParallelForks
            maxHeapSize = project.test.maxHeapSize

            systemProperties = [
                // Allow tests to set @Execution(ExecutionMode.CONCURRENT)
                'junit.jupiter.execution.parallel.enabled': 'true',
            ]

            // Limit the number of concurrent tests within a single test class.
            // See also usages of numberThreads.
            if (project.hasProperty('parallelExecutionsPerThread')) {
                int parallelExecutionsPerThread = project.property('parallelExecutionsPerThread').toString() as int
                systemProperties = systemProperties + [
                    'junit.jupiter.execution.parallel.config.strategy': 'fixed',
                    'junit.jupiter.execution.parallel.config.fixed.parallelism': parallelExecutionsPerThread,
                ]
            }
        }
        integrationTestJava.configure {
            mustRunAfter project.tasks.named('check')
            dependsOn project.tasks.matching { it.name == 'assemble' }
        }
    }
}

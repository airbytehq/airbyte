import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.testing.Test

class AirbyteIntegrationTestJavaPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.sourceSets {
            integrationTest {
                java {
                    srcDir 'src/test-integration/java'
                }
                resources {
                    srcDir 'src/test-integration/resources'
                }
            }
        }
        project.test.dependsOn('compileIntegrationTestJava')

        project.configurations {
            integrationTestImplementation.extendsFrom testImplementation
            integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
        }

        project.task('integrationTest', type: Test) {
            testClassesDirs += project.sourceSets.integrationTest.output.classesDirs
            classpath += project.sourceSets.integrationTest.runtimeClasspath

            useJUnitPlatform()
            testLogging() {
                events "passed", "failed"
                exceptionFormat "full"
            }

            dependsOn project.airbyteDocker
            mustRunAfter project.test
        }

    }
}

import org.gradle.api.Plugin
import org.gradle.api.Project
import org.gradle.api.tasks.testing.Test

class AirbyteIntegrationTestJavaPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.sourceSets {
            integrationTestJava {
                java {
                    srcDir 'src/test-integration/java'
                }
                resources {
                    srcDir 'src/test-integration/resources'
                }
            }
        }
        project.test.dependsOn('compileIntegrationTestJavaJava')

        project.configurations {
            integrationTestJavaImplementation.extendsFrom testImplementation
            integrationTestJavaRuntimeOnly.extendsFrom testRuntimeOnly
        }

        project.task('integrationTestJava', type: Test) {
            testClassesDirs += project.sourceSets.integrationTestJava.output.classesDirs
            classpath += project.sourceSets.integrationTestJava.runtimeClasspath

            useJUnitPlatform {
                // todo (cgardens) - figure out how to de-dupe this exclusion with the one in build.gradle.
                excludeTags 'log4j2-config', 'logger-client', 'cloud-storage'
            }

            testLogging() {
                events "passed", "failed"
                exceptionFormat "full"
                showStandardStreams = true
            }

            outputs.upToDateWhen { false }

            if (project.hasProperty('airbyteDocker')) {
                dependsOn project.airbyteDocker
            }
            if (project.hasProperty('buildDockerImage-all')) {
                dependsOn project.'buildDockerImage-all'
            }

            maxHeapSize = '3g'

            mustRunAfter project.test

            // This is needed to make the destination-snowflake tests succeed - https://github.com/snowflakedb/snowflake-jdbc/issues/589#issuecomment-983944767
            jvmArgs = ["--add-opens=java.base/java.nio=ALL-UNNAMED"]
        }

        // make sure we create the integrationTest task once in case a standard source test was already initialized
        if(!project.hasProperty('integrationTest')) {
            project.task('integrationTest')
        }

        project.integrationTest.dependsOn(project.integrationTestJava)
    }
}

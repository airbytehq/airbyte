import org.gradle.api.Plugin
import org.gradle.api.Project

class AirbyteSourceAcceptanceTestPlugin implements Plugin<Project> {
    void apply(Project project) {
        project.task('sourceAcceptanceTest') {
            doFirst {
                project.exec {
                    def targetMountDirectory = "/test_input"
                    def args = [
                        'docker', 'run', '--rm', '-i',
                        // provide access to the docker daemon
                        '-v', '/var/run/docker.sock:/var/run/docker.sock',
                        // A container within a container mounts from the host filesystem, not the parent container.
                        // this forces /tmp to be the same directory for host, parent container, and child container.
                        '-v', '/tmp:/tmp',
                        // mount the project dir. all provided input paths must be relative to that dir.
                        '-v', "${project.projectDir.absolutePath}:${targetMountDirectory}",
                        '-w', "$targetMountDirectory",
                        '-e', "AIRBYTE_SAT_CONNECTOR_DIR=${project.projectDir.absolutePath}",
                        'airbyte/source-acceptance-test:dev',
                        '-p', 'integration_tests.acceptance',
                    ]
                    commandLine args
                }
            }

            outputs.upToDateWhen { false }
        }

        project.sourceAcceptanceTest.dependsOn(':airbyte-integrations:bases:source-acceptance-test:airbyteDocker')
        project.sourceAcceptanceTest.dependsOn(project.build)
        project.sourceAcceptanceTest.dependsOn(project.airbyteDocker)
        if (project.hasProperty('airbyteDockerTest')){
            project.sourceAcceptanceTest.dependsOn(project.airbyteDockerTest)
        }

        // make sure we create the integrationTest task once
        if (!project.tasks.findByName('integrationTest')) {
            project.task('integrationTest')
        }

        project.integrationTest.dependsOn(project.sourceAcceptanceTest)
    }
}


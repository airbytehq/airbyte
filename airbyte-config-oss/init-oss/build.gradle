plugins {
    id 'java-library'
    id "de.undercouch.download" version "5.4.0"

}

dependencies {
    annotationProcessor libs.bundles.micronaut.annotation.processor
    api libs.bundles.micronaut.annotation

    implementation 'commons-cli:commons-cli:1.4'
    implementation project(':airbyte-commons-cli')
    implementation project(':airbyte-config-oss:config-models-oss')
    implementation libs.airbyte.protocol
    implementation project(':airbyte-json-validation')
    implementation libs.lombok
    implementation libs.micronaut.cache.caffeine

    testImplementation project(':airbyte-test-utils')
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'
}

task downloadConnectorRegistry(type: Download) {
    src 'https://connectors.airbyte.com/files/registries/v0/oss_registry.json'
    dest new File(projectDir, 'src/main/resources/seed/oss_registry.json')
    overwrite true
}

task downloadSpecSecretMask(type: Download) {
    src 'https://connectors.airbyte.com/files/registries/v0/specs_secrets_mask.yaml'
    dest new File(project(":airbyte-commons").projectDir, 'src/main/resources/seed/specs_secrets_mask.yaml')
    overwrite true
}

processResources.dependsOn(downloadConnectorRegistry)
processResources.dependsOn(downloadSpecSecretMask)

Task publishArtifactsTask = getPublishArtifactsTask("$rootProject.ext.version", project)

plugins {
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '6.6.1'
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
task buildImage(type: DockerBuildImage) {
    inputDir = projectDir
    images.add('dataline/integration-singer-bigquery-destination:dev')
}

sourceSets {
    integrationTest {
        java {
            srcDir("src/test-integration/java")
        }
        resources {
            srcDir("src/test-integration/resources")
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    integrationTestImplementation 'org.apache.commons:commons-dbcp2:2.7.0'
    integrationTestImplementation 'com.google.cloud:google-cloud-bigquery:1.117.0'
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind'
    integrationTestImplementation 'org.apache.commons:commons-text:1.9'

    integrationTestImplementation project(':dataline-workers')
}

import groovy.json.JsonSlurper

def credFile = project.file("config/credentials.json")
def jsonSlurper = new JsonSlurper()
def creds = [:]

try {
    creds = jsonSlurper.parse(credFile)
} catch(Exception e) {
    // allow builds without credentials to work (integrationTest would still fail)
}

task integrationTest(type: Test) {
    environment "GOOGLE_CLOUD_PROJECT", creds["project_id"]
    environment "GOOGLE_APPLICATION_CREDENTIALS", credFile.absolutePath

    testClassesDirs += sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    testLogging() {
        events "passed", "failed"
        exceptionFormat "full"
    }
    mustRunAfter test
}
integrationTest.dependsOn(buildImage)

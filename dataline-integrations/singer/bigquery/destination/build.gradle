plugins {
    id 'java'
}

sourceSets {
    integrationTest {
        java {
            srcDir("src/test-integration/java")
        }
        resources {
            srcDir("src/test-integration/resources")
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    integrationTestImplementation 'org.apache.commons:commons-dbcp2:2.7.0'
    integrationTestImplementation 'com.google.cloud:google-cloud-bigquery:1.117.0'
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind'
    integrationTestImplementation 'org.apache.commons:commons-text:1.9'

    integrationTestImplementation project(':dataline-workers')
}

task integrationTest(type: Test) {
    environment "GOOGLE_CLOUD_PROJECT", "eco-hangar-279116" // todo: get this from the cred file
    environment "GOOGLE_APPLICATION_CREDENTIALS", new File("$projectDir").absolutePath + "/config/bigquery_creds.json"

    testClassesDirs += sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    testLogging() {
        events "passed", "failed"
        exceptionFormat "full"
    }
    mustRunAfter test
}

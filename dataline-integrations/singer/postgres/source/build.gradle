plugins {
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '6.6.1'
}

sourceSets {
    integrationTest {
        java {
            srcDir("src/test-integration/java")
        }
        resources {
            srcDir("src/test-integration/resources")
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    integrationTestImplementation 'org.apache.commons:commons-dbcp2:2.7.0'
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind'
    integrationTestImplementation 'org.apache.commons:commons-text:1.9'
    integrationTestImplementation "org.testcontainers:postgresql:1.14.3"
    integrationTestImplementation "org.postgresql:postgresql:42.2.16"

    integrationTestImplementation project(':dataline-config:models')
    integrationTestImplementation project(':dataline-workers')
    integrationTestImplementation project(':dataline-test-utils')
    integrationTestImplementation project(':dataline-singer')
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
task buildImage(type: DockerBuildImage) {
    inputDir = file('.')
    images.add('dataline/integration-singer-postgres-source:latest')
}

task integrationTest(type: Test) {
    testClassesDirs += sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    testLogging() {
        events "passed", "failed"
        exceptionFormat "full"
    }
    mustRunAfter test
}

plugins {
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '6.6.1'
}

def image = 'dataline/integration-singer-postgres-destination:dev'

task imageName {
    doLast {
        println "IMAGE $image"
    }
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
task buildImage(type: DockerBuildImage) {
    inputDir = projectDir
    images.add(image)
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testLogging() {
        events "passed", "failed"
        exceptionFormat "full"
    }
}
integrationTest.dependsOn(buildImage)

sourceSets {
    integrationTest {
        java {
            srcDir("src/test-integration/java")
        }
        resources {
            srcDir("src/test-integration/resources")
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    integrationTestImplementation 'org.apache.commons:commons-dbcp2:2.7.0'
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind'
    integrationTestImplementation 'org.apache.commons:commons-text:1.9'
    integrationTestImplementation "org.testcontainers:postgresql:1.14.3"
    integrationTestImplementation "org.postgresql:postgresql:42.2.16"

    integrationTestImplementation project(':dataline-config:models')
    integrationTestImplementation project(':dataline-workers')
    integrationTestImplementation project(':dataline-db')
}

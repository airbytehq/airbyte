plugins {
    id 'java-library'
}

configurations.all {
    resolutionStrategy {
        force 'org.codehaus.jettison:jettison:1.5.4', 'org.eclipse.jetty.websocket:websocket-client:9.4.51.v20230217', libs.jsonsmart
    }
}

dependencies {
    implementation project(':airbyte-config-oss:config-models-oss')
    implementation libs.airbyte.protocol
    implementation project(':airbyte-integrations:bases:base-java')
    implementation files(project(':airbyte-integrations:bases:base-java').airbyteDocker.outputs)

    implementation 'org.apache.commons:commons-csv:1.10.0'
    implementation 'com.github.alexmojaki:s3-stream-upload:2.2.4'

    implementation ('org.apache.parquet:parquet-avro:1.13.1') { exclude group: 'org.slf4j', module: 'slf4j-log4j12'}
    implementation ('com.github.airbytehq:json-avro-converter:1.1.0') { exclude group: 'ch.qos.logback', module: 'logback-classic'}

    // forcing latest to avoid https://nvd.nist.gov/vuln/detail/CVE-2023-1436
    implementation 'org.codehaus.jettison:jettison:1.5.4'
    // parquet
    implementation ('org.apache.hadoop:hadoop-common:3.3.5') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'com.github.pjfanning', module: 'jersey-json'
        // https://nvd.nist.gov/vuln/detail/CVE-2023-1436
        exclude group: 'org.codehaus.jettison', module: 'jettison'
        // https://nvd.nist.gov/vuln/detail/CVE-2022-3509 and forcing latest protbuf
        exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_7'
        exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-guava'
        // https://nvd.nist.gov/vuln/detail/CVE-2023-26048
        exclude group: 'org.eclipse.jetty', module: 'jetty-server'
        // https://nvd.nist.gov/vuln/detail/CVE-2023-1370
        exclude group: 'net.minidev', module: 'json-smart'
    }
    implementation ('org.apache.hadoop:hadoop-aws:3.3.5') { exclude group: 'org.slf4j', module: 'slf4j-log4j12'}
    implementation ('org.jetbrains.kotlin:kotlin-stdlib:1.8.21')
    implementation ('org.apache.hadoop:hadoop-mapreduce-client-core:3.3.5') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'org.slf4j', module: 'slf4j-reload4j'
        // https://nvd.nist.gov/vuln/detail/CVE-2019-20444
        exclude group: 'io.netty', module: 'netty'
        // https://nvd.nist.gov/vuln/detail/CVE-2022-3509 and forcing latest protbuf
        exclude group: 'org.apache.hadoop.thirdparty', module: 'hadoop-shaded-protobuf_3_7'
        // https://nvd.nist.gov/vuln/detail/CVE-2023-26048 via org.eclipse.jetty:jetty-io:9.4.48.v20220622
        exclude group: 'org.eclipse.jetty.websocket', module: 'websocket-client'
        // https://nvd.nist.gov/vuln/detail/CVE-2022-24329
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib'
    }

    implementation ('org.apache.parquet:parquet-avro:1.13.1') { exclude group: 'org.slf4j', module: 'slf4j-log4j12'}
    implementation ('com.github.airbytehq:json-avro-converter:1.1.0') { exclude group: 'ch.qos.logback', module: 'logback-classic'}

    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.xerial.snappy:snappy-java:1.1.9.1'
    testImplementation "org.mockito:mockito-inline:4.1.0"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation group: 'com.hadoop.gplcompression', name: 'hadoop-lzo', version: '0.4.20'
}

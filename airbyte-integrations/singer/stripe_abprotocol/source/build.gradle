import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
plugins {
    id 'java'
    id 'com.bmuschko.docker-remote-api'
}

// TODO: rename without abprotocol when we can get rid of singer protocol
def image = 'airbyte/source-stripe-abprotocol-singer:dev'

sourceSets {
    integrationTest {
        java {
            srcDir 'src/test-integration/java'
        }
        resources {
            srcDir 'src/test-integration/resources'
        }
    }
}
test.dependsOn('compileIntegrationTestJava')

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    integrationTestImplementation 'org.apache.commons:commons-dbcp2:2.7.0'
    integrationTestImplementation 'com.fasterxml.jackson.core:jackson-databind'
    integrationTestImplementation 'org.apache.commons:commons-text:1.9'
    integrationTestImplementation "com.stripe:stripe-java:20.6.0"

    integrationTestImplementation project(':airbyte-workers')
    integrationTestImplementation project(':airbyte-config:models')
}

task imageName {
    doLast {
        println "IMAGE $image"
    }
}

task buildImage(type: DockerBuildImage) {
    inputDir = projectDir
    images.add(image)
    dependsOn ":airbyte-integrations:singer:base-singer:buildImage"
}

task integrationTest(type: Test) {
    testClassesDirs += sourceSets.integrationTest.output.classesDirs
    classpath += sourceSets.integrationTest.runtimeClasspath

    useJUnitPlatform()
    testLogging() {
        events "passed", "failed"
        exceptionFormat "full"
    }

    mustRunAfter test
    dependsOn(buildImage)
}


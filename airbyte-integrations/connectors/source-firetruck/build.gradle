plugins {
    id 'application'
}

application {
    mainClass = 'io.airbyte.integrations.source.oracle.OracleSource'
    applicationDefaultJvmArgs = [
            '-XX:+ExitOnOutOfMemoryError',
            '-XX:InitialRAMPercentage=75.0',
            '-XX:MaxRAMPercentage=75.0',
    ]
}


processResources {
    from("$projectDir") {
        include 'metadata.yaml'
        duplicatesStrategy DuplicatesStrategy.EXCLUDE
    }
}

tasks.named('spotbugsTest').configure {
    enabled = false
}

tasks.named('check').configure {
    dependsOn tasks.matching { it.name ==~ /(compile|spotbugs)[a-zA-Z]*Java/ }
}

dependencies {
    def vBulkCDK = '0.20'
    def vMicronaut = '4.6.1'

    ksp "io.micronaut:micronaut-inject-kotlin:${vMicronaut}"

    implementation "io.airbyte.bulk-cdk:bulk-cdk-core-base:${vBulkCDK}"
    implementation "io.airbyte.bulk-cdk:bulk-cdk-core-extract:${vBulkCDK}"
    implementation "io.airbyte.bulk-cdk:bulk-cdk-toolkit-extract-jdbc:${vBulkCDK}"
    implementation 'com.oracle.database.jdbc:ojdbc11-production:23.4.0.24.05'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.77'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.77'
    implementation 'org.bouncycastle:bctls-jdk18on:1.77'

    kspTest "io.micronaut:micronaut-inject-kotlin:${vMicronaut}"

    testImplementation platform('org.testcontainers:testcontainers-bom:1.20.1')
    testImplementation testFixtures("io.airbyte.bulk-cdk:bulk-cdk-core-extract:${vBulkCDK}")
    testImplementation testFixtures("io.airbyte.bulk-cdk:bulk-cdk-toolkit-extract-jdbc:${vBulkCDK}")
    testImplementation 'org.testcontainers:oracle-xe'
}

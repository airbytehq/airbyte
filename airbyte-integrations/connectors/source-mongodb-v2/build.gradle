plugins {
    id 'application'
    id 'airbyte-docker'
    id 'airbyte-integration-test-java'
    id 'airbyte-connector-acceptance-test'
    alias(libs.plugins.kotlin.jvm)
}

application {
    mainClass = 'io.airbyte.integrations.source.mongodb.MongoDbSource'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

configurations {
    debeziumTest.extendsFrom testImplementation
}

sourceSets {
    debeziumTest {
        kotlin {
            srcDirs('src/test/debezium')
        }
    }
}

dependencies {
    implementation libs.slf4j.api
    implementation libs.jackson.databind
    implementation project(':airbyte-db:db-lib')
    implementation project(':airbyte-integrations:bases:base-java')
    implementation project(':airbyte-integrations:bases:debezium')
    implementation libs.airbyte.protocol

    implementation libs.mongo.driver.sync

    testImplementation testFixtures(project(':airbyte-integrations:bases:debezium'))
    testImplementation libs.kotlinx.cli
    testImplementation libs.connectors.testcontainers.mongodb

    integrationTestJavaImplementation libs.apache.commons.lang
    integrationTestJavaImplementation project(':airbyte-integrations:bases:standard-source-test')
    integrationTestJavaImplementation project(':airbyte-integrations:connectors:source-mongodb-v2')

    debeziumTestImplementation libs.bundles.debezium.bundle
    debeziumTestImplementation libs.bundles.slf4j
    debeziumTestImplementation libs.slf4j.simple
    debeziumTestImplementation libs.kotlinx.cli.jvm
    debeziumTestImplementation libs.spotbugs.annotations
}

/*
 * Executes the script that generates test data and inserts it into the provided database/collection.
 *
 * To execute this task, use the following command:
 *
 * ./gradlew :airbyte-integrations:connectors:source-mongodb-v2:generateTestData -PconnectionString=<connection string> -PdatabaseName=<database name> -PcollectionName=<collection name> -Pusername=<username>
 *
 * Optionally, you can provide -PnumberOfDocuments to change the number of generated documents from the default (10,000).
 */
tasks.register('generateTestData', JavaExec) {
    def arguments = []

    if(project.hasProperty('collectionName')) {
        arguments.addAll(['--collection-name', collectionName])
    }
    if(project.hasProperty('connectionString')) {
        arguments.addAll(['--connection-string', connectionString])
    }
    if(project.hasProperty('databaseName')) {
        arguments.addAll(['--database-name', databaseName])
    }
    if (project.hasProperty('numberOfDocuments')) {
        arguments.addAll(['--number', numberOfDocuments])
    }
    if(project.hasProperty('username')) {
        arguments.addAll(['--username', username])
    }

    classpath = sourceSets.test.runtimeClasspath
    main 'io.airbyte.integrations.source.mongodb.MongoDbInsertClient'
    standardInput = System.in
    args arguments
}

/**
 * Executes the Debezium MongoDB Connector test harness.
 *
 * To execute this task, use the following command:
 *
 * ./gradlew :airbyte-integrations:connectors:source-mongodb-v2:debeziumTest -PconnectionString=<connection string> -PdatabaseName=<database name> -PcollectionName=<collection name> -Pusername=<username>
 */
tasks.register('debeziumTest', JavaExec) {
    def arguments = []

    if(project.hasProperty('collectionName')) {
        arguments.addAll(['--collection-name', collectionName])
    }
    if(project.hasProperty('connectionString')) {
        arguments.addAll(['--connection-string', connectionString])
    }
    if(project.hasProperty('databaseName')) {
        arguments.addAll(['--database-name', databaseName])
    }
    if(project.hasProperty('username')) {
        arguments.addAll(['--username', username])
    }

    classpath = sourceSets.debeziumTest.runtimeClasspath
    main 'io.airbyte.integrations.source.mongodb.DebeziumMongoDbConnectorTest'
    standardInput = System.in
    args arguments
}

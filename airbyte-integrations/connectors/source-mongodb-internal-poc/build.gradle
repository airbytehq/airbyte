plugins {
    id 'application'
    id 'airbyte-docker'
    id 'airbyte-integration-test-java'
    id 'airbyte-connector-acceptance-test'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
}

application {
    mainClass = 'io.airbyte.integrations.source.mongodb.internal.MongoDbSource'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

dependencies {
    implementation libs.slf4j.api
    implementation libs.jackson.databind
    implementation project(':airbyte-db:db-lib')
    implementation project(':airbyte-integrations:bases:base-java')
    implementation project(':airbyte-integrations:bases:debezium')
    implementation libs.airbyte.protocol
    implementation files(project(':airbyte-integrations:bases:base-java').airbyteDocker.outputs)

    implementation 'org.mongodb:mongodb-driver-sync:4.10.2'

    testImplementation testFixtures(project(':airbyte-integrations:bases:debezium'))
    testImplementation "org.jetbrains.kotlinx:kotlinx-cli:0.3.5"
    testImplementation libs.connectors.testcontainers.mongodb

    integrationTestJavaImplementation project(':airbyte-integrations:bases:standard-source-test')
    integrationTestJavaImplementation project(':airbyte-integrations:connectors:source-mongodb-internal-poc')
    integrationTestJavaImplementation files(project(':airbyte-integrations:bases:base-java').airbyteDocker.outputs)
}

/*
 * Executes the script that generates test data and inserts it into the provided database/collection.
 *
 * To execute this task, use the following command:
 *
 * ./gradlew :airbyte-integrations:connectors:source-mongodb-internal-poc:generateTestData -PconnectionString=<connection string> -PreplicaSet=<replica set> -PdatabaseName=<database name> -PcollectionName=<collection name> -Pusername=<username>
 *
 * Optionally, you can provide -PnumberOfDocuments to change the number of generated documents from the default (10,000).
 */
tasks.register('generateTestData', JavaExec) {
    def arguments = []

    if(project.hasProperty('collectionName')) {
        arguments.addAll(['--collection-name', collectionName])
    }
    if(project.hasProperty('connectionString')) {
        arguments.addAll(['--connection-string', connectionString])
    }
    if(project.hasProperty('databaseName')) {
        arguments.addAll(['--database-name', databaseName])
    }
    if (project.hasProperty('numberOfDocuments')) {
        arguments.addAll(['--number', numberOfDocuments])
    }
    if(project.hasProperty('replicaSet')) {
        arguments.addAll(['--replica-set', replicaSet])
    }
    if(project.hasProperty('username')) {
        arguments.addAll(['--username', username])
    }

    classpath = sourceSets.test.runtimeClasspath
    main 'io.airbyte.integrations.source.mongodb.internal.MongoDbInsertClient'
    standardInput = System.in
    args arguments
}

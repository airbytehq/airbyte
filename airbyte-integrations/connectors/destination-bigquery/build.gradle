plugins {
    id 'application'
    id 'airbyte-docker'
    id 'airbyte-integration-test-java'
}

application {
    mainClass = 'io.airbyte.integrations.destination.bigquery.BigQueryDestination'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

dependencies {
    implementation 'com.google.cloud:google-cloud-bigquery:1.122.2'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-csv:1.4'
    implementation group: 'org.apache.parquet', name: 'parquet-avro', version: '1.12.0'

    implementation group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.4.5'
    implementation group: 'com.codepoetics', name: 'protonpack', version: '1.13'

    implementation project(':airbyte-config-oss:config-models-oss')
    implementation project(':airbyte-integrations:bases:base-java')
    implementation libs.airbyte.protocol
    implementation project(':airbyte-integrations:bases:base-java-s3')
    implementation project(':airbyte-integrations:connectors:destination-gcs')
    implementation ('com.github.airbytehq:json-avro-converter:1.1.0') { exclude group: 'ch.qos.logback', module: 'logback-classic'}

    testImplementation project(':airbyte-integrations:bases:standard-destination-test')

    integrationTestJavaImplementation project(':airbyte-integrations:bases:standard-destination-test')
    integrationTestJavaImplementation project(':airbyte-integrations:connectors:destination-bigquery')
    integrationTestJavaImplementation project(':airbyte-db:db-lib')

    implementation files(project(':airbyte-integrations:bases:base-java').airbyteDocker.outputs)
}

tasks.named("airbyteDocker") {
    // this is really inefficent (because base-normalization:airbyteDocker builds 9 docker images)
    // but it's also just simple to implement
    dependsOn project(':airbyte-integrations:bases:base-normalization').airbyteDocker
}

configurations.all {
  resolutionStrategy {
    // at time of writing: deps.toml declares google-cloud-storage 2.17.2
    // which pulls in google-api-client:2.2.0
    // which conflicts with google-cloud-bigquery, which requires google-api-client:1.x
    // google-cloud-storage is OK with downgrading to anything >=1.31.1.
    force 'com.google.api-client:google-api-client:1.31.5'
  }
}

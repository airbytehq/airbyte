plugins {
    id 'application'
    id 'airbyte-java-connector'
}

airbyteJavaConnector {
    cdkVersionRequired = '0.1.7'
    features = ['db-destinations']
    useLocalCdk = false
}

airbyteJavaConnector.addCdkDependencies()

application {
    mainClass = 'io.airbyte.integrations.destination.bigquery.BigQueryDestination'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0',
                                 '-XX:NativeMemoryTracking=detail', '-XX:+UnlockDiagnosticVMOptions',
                                 '-XX:GCLockerRetryAllocationCount=100',
//            '-Djava.rmi.server.hostname=localhost',
//            '-Dcom.sun.management.jmxremote=true',
//            '-Dcom.sun.management.jmxremote.port=6000',
//            '-Dcom.sun.management.jmxremote.rmi.port=6000',
//            '-Dcom.sun.management.jmxremote.local.only=false',
//            '-Dcom.sun.management.jmxremote.authenticate=false',
//            '-Dcom.sun.management.jmxremote.ssl=false'
    ]
}

airbyteJavaConnector.addCdkDependencies()

dependencies {
    implementation project(':airbyte-integrations:connectors:destination-gcs')

    implementation 'com.google.cloud:google-cloud-bigquery:2.31.1'
    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'org.apache.commons:commons-csv:1.4'
    implementation 'org.apache.commons:commons-text:1.10.0'

    implementation group: 'com.google.cloud', name: 'google-cloud-storage', version: '2.4.5'
    implementation group: 'com.codepoetics', name: 'protonpack', version: '1.13'

    implementation project(':airbyte-config-oss:config-models-oss')
    implementation (libs.airbyte.protocol) {
        exclude group: 'io.airbyte', module: 'airbyte-commons'
    }
    implementation project(':airbyte-integrations:bases:base-typing-deduping')
    // implementation ('com.github.airbytehq:json-avro-converter:1.1.0') { exclude group: 'ch.qos.logback', module: 'logback-classic'}

    integrationTestJavaImplementation project(':airbyte-integrations:bases:base-typing-deduping-test')

    integrationTestJavaImplementation project(':airbyte-integrations:connectors:destination-bigquery')
}

configurations.all {
  resolutionStrategy {
    // at time of writing: deps.toml declares google-cloud-storage 2.17.2
    // which pulls in google-api-client:2.2.0
    // which conflicts with google-cloud-bigquery, which requires google-api-client:1.x
    // google-cloud-storage is OK with downgrading to anything >=1.31.1.
    force 'com.google.api-client:google-api-client:1.31.5'
  }
}

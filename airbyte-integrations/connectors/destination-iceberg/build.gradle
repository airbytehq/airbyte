plugins {
    id 'application'
    id 'airbyte-java-connector'
}

airbyteJavaConnector {
    cdkVersionRequired = '0.2.0'
    features = ['db-destinations']
    useLocalCdk = false
}

//remove once upgrading the CDK version to 0.4.x or later
java {
    compileJava {
        options.compilerArgs.remove("-Werror")
    }
}

airbyteJavaConnector.addCdkDependencies()

application {
    mainClass = 'io.airbyte.integrations.destination.iceberg.IcebergDestination'
}

dependencies {

    implementation('org.apache.spark:spark-sql_2.13:3.5.1') {
        exclude(group: 'org.apache.hadoop', module: 'hadoop-common')
    }
    implementation('org.apache.spark:spark-hive_2.13:3.5.1') {
        exclude(group: 'org.apache.hadoop', module: 'hadoop-common')
    }
    implementation 'org.apache.iceberg:iceberg-spark-runtime-3.5_2.13:1.5.2'

    // force awssdk version required by Iceberg
    implementation "software.amazon.awssdk:utils:2.24.5"
    implementation "software.amazon.awssdk:url-connection-client:2.24.5"
    implementation "software.amazon.awssdk:s3:2.24.5"
    implementation "software.amazon.awssdk:glue:2.24.5"
    implementation "software.amazon.awssdk:dynamodb:2.24.5"
    implementation "software.amazon.awssdk:kms:2.24.5"
    implementation "software.amazon.awssdk:sts:2.24.5"
    implementation "software.amazon.awssdk:sdk-core:2.24.5"
    implementation "software.amazon.awssdk:aws-core:2.24.5"

    implementation "org.apache.hadoop:hadoop-aws:3.3.2"
    implementation "org.apache.hadoop:hadoop-client-api:3.3.2"
    implementation "org.apache.hadoop:hadoop-client-runtime:3.3.2"
    implementation "org.postgresql:postgresql:42.5.0"
    implementation "commons-collections:commons-collections:3.2.2"

    implementation('org.antlr:antlr4-runtime') {
        version {
            strictly('4.9.3')
        }
    }

    testImplementation libs.testcontainers.postgresql
    integrationTestJavaImplementation libs.testcontainers.postgresql

    testImplementation 'org.mockito:mockito-inline:4.7.0'
}

test {
    jvmArgs = ['--add-opens=java.base/sun.nio.ch=ALL-UNNAMED', '--add-opens=java.base/java.nio=ALL-UNNAMED']
}

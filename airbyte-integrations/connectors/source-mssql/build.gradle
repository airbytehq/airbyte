import org.jsonschema2pojo.SourceType

plugins {
    id 'application'
    id 'airbyte-java-connector'
    id 'org.jsonschema2pojo' version '1.2.1'
}

airbyteJavaConnector {
    cdkVersionRequired = '0.7.7'
    features = ['db-sources']
    useLocalCdk = false
}

configurations.all {
    resolutionStrategy {
        force libs.jooq
    }
}


application {
    mainClass = 'io.airbyte.integrations.source.mssql.MssqlSource'
    applicationDefaultJvmArgs = ['-XX:+ExitOnOutOfMemoryError', '-XX:MaxRAMPercentage=75.0']
}

dependencies {
    implementation libs.postgresql

    implementation libs.debezium.sqlserver
    implementation libs.debezium.embedded
    implementation 'com.microsoft.sqlserver:mssql-jdbc:10.2.1.jre8'
    implementation 'org.codehaus.plexus:plexus-utils:3.4.2'

    testImplementation 'org.apache.commons:commons-lang3:3.11'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.awaitility:awaitility:4.2.0'

    testImplementation libs.testcontainers.mssqlserver
    testFixturesImplementation libs.testcontainers.mssqlserver
}

jsonSchema2Pojo {
    sourceType = SourceType.YAMLSCHEMA
    source = files("${sourceSets.main.output.resourcesDir}/internal_models")
    targetDirectory = new File(project.buildDir, 'generated/src/gen/java/')
    removeOldOutput = true

    targetPackage = 'io.airbyte.integrations.source.mssql.internal.models'

    useLongIntegers = true
    generateBuilders = true
    includeConstructors = false
    includeSetters = true
}

plugins {
    id "base"
    id "com.github.node-gradle.node" version "2.2.4"
}

node {
    download = true
    version = "14.11.0"
}

assemble.dependsOn(npmInstall)

task testScaffoldTemplates

def addScaffoldTemplateTask(name, scaffoldParams=[]) {
    def taskName = "testScaffoldTemplate_${name}"
    def outputDir = "airbyte-integrations/connectors/source-scaffold-${name}"

   def task = tasks.create(taskName) {
        inputs.files rootProject.fileTree("airbyte-integrations/connector-templates/${name}")

        doLast {
            exec {
                workingDir rootDir
                commandLine 'rm', '-rf', outputDir
            }
            exec {
                workingDir rootDir
                def cmd = ['./tools/integrations/manage.sh', 'scaffold', name, "scaffold-${name}"]
                cmd.addAll(scaffoldParams)
                commandLine cmd
            }
        }

        outputs.dir rootProject.file(outputDir)
    }

    testScaffoldTemplates.dependsOn task
}

addScaffoldTemplateTask('source-python')
// TODO: enable Singer template testing
//addScaffoldTask('source-python-singer', ['tap-exchangeratesapi'])

plugins {
    id "base"
    id "com.github.node-gradle.node" version "2.2.4"
}

def nodeVersion = System.getenv('NODE_VERSION') ?: '16.13.0'

node {
    download = true
    version = nodeVersion
}

assemble.dependsOn(npmInstall)

task testScaffoldTemplates

def addScaffoldTemplateTask(name, packageName, outputDirName, scaffoldParams=[]) {
    def taskName = "testScaffoldTemplate_${name}"
    def outputDir = "airbyte-integrations/connectors/${outputDirName}"

   def task = tasks.create(taskName) {
        inputs.files rootProject.fileTree("airbyte-integrations/connector-templates/")

        doLast {
            exec {
                workingDir rootDir
                commandLine 'rm', '-rf', outputDir
            }
            exec {
                workingDir rootDir
                def cmd = ['./tools/integrations/manage.sh', 'scaffold', name, packageName, '02277fdd-35e6-4eda-8302-1ea07f801007']
                cmd.addAll(scaffoldParams)
                commandLine cmd
            }
        }

        outputs.dir rootProject.file(outputDir)
    }

    testScaffoldTemplates.dependsOn task
}

addScaffoldTemplateTask('Python Source', 'scaffold-source-python', 'source-scaffold-source-python')
addScaffoldTemplateTask('Python HTTP API Source', 'scaffold-source-http', 'source-scaffold-source-http')
addScaffoldTemplateTask('Java JDBC Source', 'scaffold-java-jdbc', 'source-scaffold-java-jdbc')
addScaffoldTemplateTask('Python Destination', 'scaffold-destination-python', 'destination-scaffold-destination-python')
// TODO: enable Singer template testing
//addScaffoldTask('source-python-singer', ['tap-exchangeratesapi'])

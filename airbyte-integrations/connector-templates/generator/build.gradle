plugins {
    id "base"
    id "com.github.node-gradle.node" version "2.2.4"
}

def nodeVersion = System.getenv('NODE_VERSION') ?: '14.11.0'

node {
    download = true
    version = nodeVersion
}

assemble.dependsOn(npmInstall)

task testScaffoldTemplates

def addScaffoldTemplateTask(name, packageName,scaffoldParams=[]) {
    def taskName = "testScaffoldTemplate_${name}"
    def outputDir = "airbyte-integrations/connectors/source-${packageName}"

   def task = tasks.create(taskName) {
        inputs.files rootProject.fileTree("airbyte-integrations/connector-templates/")

        doLast {
            exec {
                workingDir rootDir
                commandLine 'rm', '-rf', outputDir
            }
            exec {
                workingDir rootDir
                def cmd = ['./tools/integrations/manage.sh', 'scaffold', name, packageName]
                cmd.addAll(scaffoldParams)
                commandLine cmd
            }
        }

        outputs.dir rootProject.file(outputDir)
    }

    testScaffoldTemplates.dependsOn task
}

addScaffoldTemplateTask('Python Source', 'scaffold-source-python')
addScaffoldTemplateTask('Python HTTP API Source', 'scaffold-source-http')
addScaffoldTemplateTask('Java JDBC Source', 'scaffold-java-jdbc')
addScaffoldTemplateTask('Python Destination', 'scaffold-destination-python')
// TODO: enable Singer template testing
//addScaffoldTask('source-python-singer', ['tap-exchangeratesapi'])

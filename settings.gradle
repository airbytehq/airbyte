import groovy.io.FileType

rootProject.name = 'dataline'

include 'dataline-analytics'
include 'dataline-api'
include 'dataline-commons'
include ':dataline-config:models'
include ':dataline-config:init'
include ':dataline-config:persistence'
include 'dataline-db'
include 'dataline-integrations'
include 'dataline-scheduler'
include 'dataline-server'
include 'dataline-singer'
include 'dataline-webapp'
include 'dataline-workers'
include 'dataline-tests'

// include all integration subprojects
def integrations = file(rootProject.projectDir.relativePath(file("dataline-integrations")))
integrations.eachFileRecurse(FileType.FILES) { file ->
  if (file.absolutePath.endsWith("build.gradle") && file.length() != 0 && !file.absolutePath.contains("/build/")) {
    def subproject = file.absolutePath.substring(integrations.absolutePath.length() + 1)
    subproject = subproject.substring(0, subproject.length() - "/build.gradle".length())
    subproject = ":dataline-integrations:" + subproject.split("/").join(":")
    include subproject
  }
}

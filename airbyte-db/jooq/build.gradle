plugins {
    id 'java'
    id 'nu.studer.jooq' version '6.0.1'
}

dependencies {
    implementation 'org.jooq:jooq-meta:3.13.4'
    implementation 'org.jooq:jooq:3.13.4'
    implementation 'org.postgresql:postgresql:42.3.1'
    implementation "org.flywaydb:flyway-core:7.14.0"

    implementation project(':airbyte-db:lib')

    // jOOQ code generation
    implementation 'org.jooq:jooq-codegen:3.13.4'
    implementation "org.testcontainers:postgresql:1.15.3"
    // These are required because gradle might be using lower version of Jna from other
    // library transitive dependency. Can be removed if we can figure out which library is the cause.
    // Refer: https://github.com/testcontainers/testcontainers-java/issues/3834#issuecomment-825409079
    implementation 'net.java.dev.jna:jna:5.8.0'
    implementation 'net.java.dev.jna:jna-platform:5.8.0'

    // The jOOQ code generator only has access to classes added to the jooqGenerator configuration
    jooqGenerator project(':airbyte-db:lib')
}

jooq {
    version = '3.13.4'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        configsDatabase {
            generateSchemaSourceOnCompilation = true
            generationTool {
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'io.airbyte.db.instance.configs.ConfigsFlywayMigrationDatabase'
                        inputSchema = 'public'
                        excludes = 'airbyte_configs_migrations'
                    }
                    target {
                        packageName = 'io.airbyte.db.instance.configs.jooq'
                        directory = 'build/generated/configsDatabase/src/main/java'
                    }
                }
            }
        }

        jobsDatabase {
            generateSchemaSourceOnCompilation = true
            generationTool {
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'io.airbyte.db.instance.jobs.JobsFlywayMigrationDatabase'
                        inputSchema = 'public'
                        excludes = 'airbyte_jobs_migrations'
                    }
                    target {
                        packageName = 'io.airbyte.db.instance.jobs.jooq'
                        directory = 'build/generated/jobsDatabase/src/main/java'
                    }
                }
            }
        }
    }
}

sourceSets.main.java.srcDirs (
    tasks.named('generateConfigsDatabaseJooq').flatMap { it.outputDir },
    tasks.named('generateJobsDatabaseJooq').flatMap { it.outputDir }
)

sourceSets {
    main {
        java {
            srcDirs "$buildDir/generated/configsDatabase/src/main/java", "$buildDir/generated/jobsDatabase/src/main/java"
        }
    }
}

tasks.named('generateConfigsDatabaseJooq').configure {
    allInputsDeclared = true
    outputs.cacheIf { true }
}

tasks.named('generateJobsDatabaseJooq').configure {
    allInputsDeclared = true
    outputs.cacheIf { true }
}

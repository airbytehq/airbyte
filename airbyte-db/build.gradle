plugins {
    id 'java-library'
    id 'nu.studer.jooq' version '6.0.1'
}

dependencies {
    api 'org.apache.commons:commons-dbcp2:2.7.0'
    api 'org.jooq:jooq-meta:3.13.4'
    api 'org.jooq:jooq:3.13.4'
    api 'org.postgresql:postgresql:42.2.18'

    implementation project(':airbyte-protocol:models')
    implementation project(':airbyte-json-validation')
    implementation "org.flywaydb:flyway-core:7.14.0"

    // jOOQ code generation
    implementation 'org.jooq:jooq-codegen:3.13.4'
    implementation "org.testcontainers:postgresql:1.15.1"
    // The jOOQ code generator only has access to classes added to the jooqGenerator configuration
    jooqGenerator project(':airbyte-db')

    testImplementation project(':airbyte-test-utils')

    testImplementation 'org.apache.commons:commons-lang3:3.11'

    // Big Query
    implementation platform('com.google.cloud:libraries-bom:20.6.0')
    implementation('com.google.cloud:google-cloud-bigquery:1.133.1')

    // Lombok
    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor('org.projectlombok:lombok:1.18.20')
}

jooq {
    version = '3.13.4'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS

    configurations {
        configsDatabase {
            generateSchemaSourceOnCompilation = true
            generationTool {
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'io.airbyte.db.instance.configs.ConfigsFlywayMigrationDatabase'
                        inputSchema = 'public'
                        excludes = 'airbyte_configs_migrations'
                    }
                    target {
                        packageName = 'io.airbyte.db.instance.configs.jooq'
                        directory = 'build/generated/jooq/src/main/java'
                    }
                }
            }
        }

        jobsDatabase {
            generateSchemaSourceOnCompilation = true
            generationTool {
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'io.airbyte.db.instance.jobs.JobsFlywayMigrationDatabase'
                        inputSchema = 'public'
                        excludes = 'airbyte_jobs_migrations'
                    }
                    target {
                        packageName = 'io.airbyte.db.instance.jobs.jooq'
                        directory = 'build/generated/jooq/src/main/java'
                    }
                }
            }
        }
    }
}

build.dependsOn tasks.generateConfigsDatabaseJooq
build.dependsOn tasks.generateJobsDatabaseJooq

sourceSets {
    main {
        java {
            srcDirs "$projectDir/src/main/java", "$buildDir/generated/jooq/src/main/java"
        }
        resources {
            srcDir "$projectDir/src/main/resources"
        }
    }
}

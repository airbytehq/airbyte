import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "org.openapi.generator" version "5.3.1"
    id 'airbyte-python-docker'
    id 'airbyte-docker'
}

airbytePythonDocker {
    moduleDirectory 'connector_builder'
}

task generateOpenApiPythonServer(type: GenerateTask) {
    outputs.upToDateWhen { false }

    def generatedCodeDir = "$buildDir/server"
    inputSpec = "$rootDir.absolutePath/airbyte-connector-builder-server/src/main/openapi/openapi.yaml"
    outputDir = "$buildDir/airbyte_connector_builder_server"

    generatorName = "python-fastapi"
    configFile = "$projectDir/openapi/generator_config.yaml"
    templateDir = "$projectDir/openapi/templates"
    packageName = "connector_builder.generated"

    // After we generate, we're only interested in the API declaration and the generated pydantic models.
    // So we copy those from the build/ directory
    doLast {
        def sourceDir = "$generatedCodeDir/src/connector_builder/generated/"
        def targetDir = "$projectDir/connector_builder/generated"
        mkdir targetDir
        copy {
            from "$sourceDir/apis"
            include "*_interface.py", "__init__.py"
            into "$targetDir/apis"
        }
        copy {
            from "$sourceDir/models"
            include "*.py"
            into "$targetDir/models"
        }
    }
}

project.build.dependsOn(generateOpenApiPythonServer)

task prepareBuild(type: Copy) {
    from layout.projectDirectory.file("connector_builder")
    from layout.projectDirectory.file("setup.py")
    from layout.projectDirectory.file("openapi")
    from layout.projectDirectory.file("src")

    into layout.buildDirectory.dir("docker")
}

// java modules such as airbyte-server can use copyGeneratedTar to copy the files to the docker image
tasks.named("buildDockerImage") {
    dependsOn prepareBuild
    dependsOn copyDocker
}

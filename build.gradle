plugins {
    id "base"
    id "com.diffplug.spotless" version "5.1.1" apply false
}

Properties env = new Properties()
File envFile = rootProject.file(".env")
envFile.withInputStream { env.load(it) }

if (!env.containsKey("VERSION")) {
    throw new Exception("Version not specified in .env file...")
}

allprojects {
    group = "io.dataline.${rootProject.name}"
    version = env.VERSION
}

def createLicenseWith = { licence ->
    def tmp = File.createTempFile("tmp", ".tmp")
    tmp.withWriter {
        def w = it
        w.writeLine("/*")
        licence.eachLine {
            w << " * "
            w.writeLine(it)
        }
        w.writeLine(" */")
        w.writeLine("")
    }
    return tmp
}

// Java projects common configurations
subprojects {

    if (project.name == "dataline-webapp") {
        return
    }

    apply plugin: 'java'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenCentral()
    }

    spotless {
        java {
            licenseHeaderFile createLicenseWith(rootProject.file('LICENSE'))
            googleJavaFormat('1.8')
            removeUnusedImports()
            trimTrailingWhitespace()
        }
        groovyGradle {
            target '**/*.gradle'
        }
        format 'json', {
            target "**/*.json"

            prettier()
        }
    }
    compileJava.dependsOn 'spotlessApply'

    test {
        useJUnitPlatform()
        testLogging() {
            events "failed"
            exceptionFormat "full"
        }
    }

    dependencies {
        if (project.name != "dataline-commons") {
            implementation project(':dataline-commons')
        }

        implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'

        implementation group: 'commons-io', name: 'commons-io', version: '2.7'

        implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

        // SLF4J as a facade over Log4j2 required dependencies
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.0'

        // Bridges from other logging implementations to SLF4J
        implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '1.7.30' // JUL bridge
        implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.30' //JCL bridge
        implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.30' // log4j1.2 bridge

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.4.6'
    }
}
